# $Id$
# Maintainer: Simone De Gregori <orion@runeaudio.com>

pkgbase=php
pkgname=php-rune
pkgdesc="PHP-FPM 5.5.12 (RuneAudio)"
packager="RuneAudio Team"
pkgver=5.5.12
pkgrel=1
pkgdir=${startdir}/pkg/${pkgname}
arch=('armv6h')
license=('PHP')
url='http://www.php.net'
makedepends=('sqlite' 'file'
             'libmcrypt' 'tidyhtml' 'libltdl' 'libpng' 'libjpeg' 'icu'
             'curl' 'bzip2' 'db' 'gmp' 'systemd')
#source=("http://www.php.net/distributions/${pkgbase}-${pkgver}.tar.gz"
#        "http://www.php.net/distributions/${pkgbase}-${pkgver}.tar.gz.asc"
source=("php-5.5.12.tar.gz"
        "php-5.5.12.tar.gz.asc"
        'logrotate.d.php-fpm' 'php-fpm.service' 'php-fpm.tmpfiles')
md5sums=('b6a6e9c72589c265aafb7b3353a34030'
'cbc97d6d775acce3164ec7a1819f172f'
'e036c721e5ad927cd1613c7475a58b3a'
'cc2940f5312ba42e7aa1ddfab74b84c4'
'c60343df74f8e1afb13b084d5c0e47ed'
)

build() {
	cd ${srcdir}/php-${pkgver}
	local _phpconfig="--srcdir=../php-${pkgver} \
		--config-cache \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc/php \
		--localstatedir=/var \
		--with-layout=GNU \
		--with-config-file-path=/etc/php \
		--with-config-file-scan-dir=/etc/php/conf.d \
		--disable-rpath \
		--mandir=/usr/share/man \
		"

	local _phpextensions="--disable-all \
		--disable-cgi \
		--disable-ipv6 \
		--disable-shared \
		--enable-static \
		--enable-fpm \
		--enable-pcntl \
		--enable-bcmath=static \
		--enable-cli \
		--enable-ctype \
		--enable-embed=static \
		--enable-fileinfo \
		--enable-mbstring \
		--enable-opcache \
		--enable-phar \
		--enable-posix \
		--enable-shmop \
		--enable-sockets=static \
		--enable-maintainer-zts \
		--enable-zend-signals \
		--enable-sysvmsg \
		--enable-sysvsem \
		--enable-sysvshm \
		--enable-xml \
		--enable-libxml \
		--enable-json \
		--enable-session \
		--with-bz2=static \
		--with-curl=static \
		--with-fpm-systemd \
		--with-gettext \
		--with-iconv \
		--with-mcrypt \
		--with-mhash \
		--with-openssl \
		--with-pcre-regex=/usr \
		--with-tsrm-pthreads \
		--with-zlib \
		--without-pic \
		--with-readline \
		--with-pear \
		--with-fpm-user=http \
		--with-fpm-group=http \
	"

	EXTENSION_DIR=/usr/lib/php/modules
	export EXTENSION_DIR
	PEAR_INSTALLDIR=/usr/share/pear
	export PEAR_INSTALLDIR

	cd ${srcdir}/php-${pkgver}

	# php
	mkdir ${srcdir}/build-fpm
	cd ${srcdir}/build-fpm
	ln -s ../php-${pkgver}/configure
	./configure ${_phpconfig} ${_phpextensions}
	make
}

check() {
 	cd ${srcdir}/build-fpm
 	export SNMP_TIMEOUT=1
 	export SNMP_RETRIES=0
 	printf "n\n" | make test
 	echo
}

package() {
	pkgdesc='RuneAudio Optimized FastCGI Process Manager for PHP'
	depends=('pcre' 'libxml2' 'bzip2' 'curl' 'systemd')
	backup=('etc/php/php.ini')
	backup=('etc/php/php-fpm.conf')
	backup=('etc/php/pear.conf')
	install='php-fpm.install'

	cd ${srcdir}/build-fpm
	make -j1 INSTALL_ROOT=${pkgdir} install
	install -d -m755 ${pkgdir}/usr/share/pear
	# install RuneAudio php configuration
	rm -rf ${pkgdir}/etc/php
	cp -r ${startdir}/php-rune-config/ ${pkgdir}/etc/php
	chmod 755 ${pkgdir}/etc/php
	install -D -m755 ${srcdir}/build-fpm/sapi/fpm/php-fpm ${pkgdir}/usr/bin/php-fpm
	install -D -m644 ${srcdir}/build-fpm/sapi/fpm/php-fpm.8 ${pkgdir}/usr/share/man/man8/php-fpm.8
	install -D -m644 ${srcdir}/logrotate.d.php-fpm ${pkgdir}/etc/logrotate.d/php-fpm
	install -D -m644 ${srcdir}/php-fpm.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/php-fpm.conf
	install -D -m644 ${srcdir}/php-fpm.service ${pkgdir}/usr/lib/systemd/system/php-fpm.service
	make install-pear INSTALL_ROOT=${pkgdir}
	rm -rf ${pkgdir}/usr/share/pear/.{channels,depdb,depdblock,filemap,lock,registry}
	
	# remove static modules
	rm -f ${pkgdir}/usr/lib/php/modules/*.a
	# remove empty directory
	rmdir ${pkgdir}/usr/include/php/include
}

